apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 7
        versionName "1.6"
        vectorDrawables.useSupportLibrary = true
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles fileTree(dir: 'proguard', include: ['*.pro']).asList().toArray()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group.toString()
            artifactId project.name
            version '1.0'
            artifact "$buildDir/outputs/aar/mmui-release.aar"

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    if (dependency.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }

    //publish to filesystem repo
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    // material
    implementation 'com.google.android.material:material:1.3.0'
    // calligraphy
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'
    // av loading indicator
    implementation 'com.wang.avi:library:2.1.3'
    // joda date time helper
    implementation 'joda-time:joda-time:2.10.5'
    // push down anim
    implementation ( 'com.github.thekhaeng:pushdown-anim-click:1.1.1' ){
        exclude group: 'com.android.support'
    }
    // lingver -> manage locale and language
    implementation "com.github.YarikSOffice:lingver:1.2.2"
    // glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
}